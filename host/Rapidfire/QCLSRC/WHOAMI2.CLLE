     /*-------------------------------------------------------------------------------------------*/
     /*                                                                                           */
     /* Copyright (c) 2014 Task Force IT-Consulting GmbH, Waltrop (Germany)                       */
     /* This software and the accompanying materials are made available                           */
     /* under the terms of the GNU General Public License version 2.0 (GPLv2)                     */
     /* which accompanies this distribution and is available at                                   */
     /* http://www.gnu.org/licenses/gpl-2.0.html                                                  */
     /*                                                                                           */
     /*-------------------------------------------------------------------------------------------*/
     /*                                                                                           */
     /* Who Am I                                                                                  */
     /*                                                                                           */
     /*-------------------------------------------------------------------------------------------*/
     /*  >>PRE-COMPILER<<                                                                         */
     /*    >>CRTCMD<< CRTCLMOD    MODULE(&LI/&OB) +                                               */
     /*                             SRCFILE(&SL/&SF) SRCMBR(&SM);                                 */
     /*    >>IMPORTANT<<                                                                          */
     /*      >>PARM<<  DBGVIEW(*ALL);                                                             */
     /*      >>PARM<<  OPTION(*EVENTF);                                                           */
     /*      >>PARM<<  TGTRLS(&TR);                                                               */
     /*    >>END-IMPORTANT<<                                                                      */
     /*    >>EXECUTE<<                                                                            */
     /*  >>END-PRE-COMPILER<<                                                                     */
     /*------------+--------+---------------------------------------------------------------------*/
     /*   Date     I Progr. I Description                                                         */
     /*------------+--------+---------------------------------------------------------------------*/
     /* 18.03.2016 I   FH   I Program created                                                     */
     /*------------+--------+---------------------------------------------------------------------*/

             PGM        PARM(&IAM)

     /* Declare variables */
             DCL        VAR(&IAM) TYPE(*CHAR) LEN(300)
             DCL        VAR(&PGM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MOD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRC) TYPE(*CHAR) LEN(256)
             DCL        VAR(&STM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGKEY) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SENDER) TYPE(*CHAR) LEN(720)

             SNDPGMMSG  MSG('Who Am I') TOPGMQ(*PRV (WHO_AM_I +
                          WHOAMI1 WHOAMI)) MSGTYPE(*RQS) +
                          KEYVAR(&MSGKEY)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&IAM) VALUE('I don''t know')
             GOTO       CMDLBL(STOP)
             ENDDO

             RCVMSG     PGMQ(*PRV (WHO_AM_I WHOAMI1 WHOAMI)) +
                          MSGKEY(&MSGKEY) SENDER(&SENDER) +
                          SENDERFMT(*LONG)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CHGVAR     VAR(&IAM) VALUE('I don''t know')
             GOTO       CMDLBL(STOP)
             ENDDO

             CHGVAR     VAR(&PGM) VALUE(%SST(&SENDER 355 10))
             CHGVAR     VAR(&MOD) VALUE(%SST(&SENDER 365 10))
             CHGVAR     VAR(&PRC) VALUE(%SST(&SENDER 375 256))
             CHGVAR     VAR(&STM) VALUE(%SST(&SENDER 645 10))

             CHGVAR     VAR(&IAM) VALUE(&STM *BCAT &PGM *BCAT &MOD +
                          *BCAT &PRC)

 STOP:       ENDPGM
