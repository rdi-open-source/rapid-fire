     A*%%TS  SD  20171220  154614  HILDEBRAND  REL-V7R3M0  5770-WDS
     A*------------------------------------------------------------------------*
     A*                                                                        *
     A* COPYRIGHT (C) 2014 TASK FORCE IT-CONSULTING GMBH, WALTROP (GERMANY)    *
     A* This software and the accompanying materials are made available        *
     A* under the terms of the GNU General Public License version 2.0 (GPLv2)  *
     A* which accompanies this distribution and is available at                *
     A* http://www.gnu.org/licenses/gpl-2.0.html                               *
     A*                                                                        *
     A*------------------------------------------------------------------------*
     A*  >>PRE-COMPILER<<                                                      *
     A*    >>CRTCMD<< CRTDSPF    FILE(&LI/&OB) +                               *
     A*                            SRCFILE(&SL/&SF) SRCMBR(&SM);               *
     A*    >>IMPORTANT<<                                                       *
     A*      >>PARM<<  OPTION(*EVENTF);                                        *
     A*    >>END-IMPORTANT<<                                                   *
     A*    >>EXECUTE<<                                                         *
     A*  >>END-PRE-COMPILER<<                                                  *
     A*------------------------------------------------------------------------*
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT
     A          R DSPF010
     A*%%TS  SD  20160829  100840  HILDEBRAND  REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF01
     A  01
     AO 02
     AO 03                                  CF04
     A                                      RTNCSRLOC(*RECNAME &DFRCD010 &DFFLD-
     A                                      010 &DFPOS010)
     A                                      RTNCSRLOC(*MOUSE &DFROW010 &DFCOL01-
     A                                      0)
     A  90 91                               CSRLOC(DFROW010   DFCOL010)
     A                                      OVERLAY
     A            DFPOS010       4S 0H
     A            DFFLD010      10A  H
     A            DFRCD010      10A  H
     A            DFROW010       3S 0H
     A            DFCOL010       3S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A  01                              1 11MSGCON(060 CON0001 *LIBL/RF0017MF)
     A                                      DSPATR(HI)
     A  02                              1 11MSGCON(060 CON0002 *LIBL/RF0017MF)
     A                                      DSPATR(HI)
     A  03                              1 11MSGCON(060 CON0003 *LIBL/RF0017MF)
     A                                      DSPATR(HI)
     A  04                              1 11MSGCON(060 CON0004 *LIBL/RF0017MF)
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  05                              1 11MSGCON(060 CON0005 *LIBL/RF0017MF)
     A                                      DSPATR(HI)
     A                                  1 72'RF0017PG'
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(020 CON0015 *LIBL/RF0017MF)
     A            DFJOB     R        O  3 23REFFLD(AREAS/XDJOB *LIBL/AREAS)
     A                                  4  2MSGCON(020 CON0009 *LIBL/RF0017MF)
     A            DFPOS     R        O  4 23REFFLD(AREAS/XDPOS *LIBL/AREAS)
     A                                      EDTCDE(Z)
     A                                  5  2MSGCON(020 CON0010 *LIBL/RF0017MF)
     A            DFARA     R        B  5 23REFFLD(AREAS/XDARA *LIBL/AREAS)
     A  01
     AO 03                                  DSPATR(UL)
     A N01N03                               DSPATR(PR)
     A N81N82 83
     AO 81N82 83                            DSPATR(RI)
     A N81N82 83
     AO 81N82 83                            DSPATR(PC)
     A                                  5 37MSGCON(035 CON0011 *LIBL/RF0017MF)
     A                                  6  2MSGCON(020 CON0034 *LIBL/RF0017MF)
     A            DFLIB     R        B  6 23REFFLD(AREAS/XDLIB *LIBL/AREAS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N81 82N83                            DSPATR(RI)
     A N81 82N83                            DSPATR(PC)
     A  01
     AO 02
     AO 03                              6 34'F4'
     A                                      COLOR(BLU)
     A                                  6 37MSGCON(035 CON0013 *LIBL/RF0017MF)
     A                                  7  2MSGCON(020 CON0012 *LIBL/RF0017MF)
     A            DFLIBL    R        B  7 23REFFLD(AREAS/XDLIBL *LIBL/AREAS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N81 82 83                            DSPATR(RI)
     A N81 82 83                            DSPATR(PC)
     A  01
     AO 02
     AO 03                              7 34'F4'
     A                                      COLOR(BLU)
     A                                  7 37MSGCON(035 CON0014 *LIBL/RF0017MF)
     A                                  8  2MSGCON(020 CON0017 *LIBL/RF0017MF)
     A            DFCCSID   R        B  8 23REFFLD(AREAS/XDCCSID *LIBL/AREAS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A  81N82N83                            DSPATR(RI)
     A  81N82N83                            DSPATR(PC)
     A                                  8 37MSGCON(035 CON0018 *LIBL/RF0017MF)
     A                                  9  2MSGCON(020 CON0016 *LIBL/RF0017MF)
     A            DFCEXT    R        B  9 23REFFLD(AREAS/XDCEXT *LIBL/AREAS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N01N02N03                       23  2MSGCON(078 CON0006 *LIBL/RF0017MF)
     A                                      COLOR(BLU)
     A  01
     AO 02
     AO 03                             23  2MSGCON(078 CON0007 *LIBL/RF0017MF)
     A                                      COLOR(BLU)
     A          R DSPF020W
     A*%%TS  SD  20010719  145903  HILDEBRAND  REL-V4R2M0  5769-PW1
     A                                      CA12
     A                                      OVERLAY
     A                                      WINDOW(10 1 3 76)
     A                                  2  1MSGCON(076 CON0008 *LIBL/RF0017MF)
     A                                      DSPATR(BL)
     A          R DSPF999S                  SFL
     A*%%TS  SD  20010425  174353  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ(276)
     A          R DSPF999C                  SFLCTL(DSPF999S)
     A*%%TS  SD  20010430  173405  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  50                                  SFLEND
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0001)
     A            DFPGMQ                    SFLPGMQ(276)
