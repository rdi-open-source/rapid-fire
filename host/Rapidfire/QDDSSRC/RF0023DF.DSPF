     A*%%TS  SD  20230628  120030  HILDEBRAND  REL-V7R5M0  5770-WDS
     A*------------------------------------------------------------------------*
     A*                                                                        *
     A* COPYRIGHT (C) 2014 TASK FORCE IT-CONSULTING GMBH, WALTROP (GERMANY)    *
     A* This software and the accompanying materials are made available        *
     A* under the terms of the GNU General Public License version 2.0 (GPLv2)  *
     A* which accompanies this distribution and is available at                *
     A* http://www.gnu.org/licenses/gpl-2.0.html                               *
     A*                                                                        *
     A*------------------------------------------------------------------------*
     A*  >>PRE-COMPILER<<                                                      *
     A*    >>CRTCMD<< CRTDSPF    FILE(&LI/&OB) +                               *
     A*                            SRCFILE(&SL/&SF) SRCMBR(&SM);               *
     A*    >>IMPORTANT<<                                                       *
     A*      >>PARM<<  OPTION(*EVENTF);                                        *
     A*    >>END-IMPORTANT<<                                                   *
     A*    >>EXECUTE<<                                                         *
     A*  >>END-PRE-COMPILER<<                                                  *
     A*------------------------------------------------------------------------*
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT
     A          R DSPF010S                  SFL
     A*%%TS  SD  20230524  140022  HILDEBRAND  REL-V7R5M0  5770-WDS
     A  63                                  SFLNXTCHG
     A            DFOPT          2A  H
     A            DFLIB     R        O 18  2REFFLD(FLDGENCLS/XILIB *LIBL/FLDGEN-
     A                                      CLS)
     A            DFFILE    R        O 18 13REFFLD(FLDGENCLS/XIFILE *LIBL/FLDGE-
     A                                      NCLS)
     A            DFFLD     R        O 18 24REFFLD(FLDGENCLS/XIFLD *LIBL/FLDGEN-
     A                                      CLS)
     A            DFTEXT    R        O 18 35REFFLD(FLDGENCLS/XITEXT *LIBL/FLDGE-
     A                                      NCLS)
     A          R DSPF010C                  SFLCTL(DSPF010S)
     A*%%TS  SD  20230524  143347  HILDEBRAND  REL-V7R5M0  5770-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0007)
     A                                      CA12
     A                                      CF01
     A                                      CF05
     A                                      CF10
     A                                      CF17
     A                                      CF18
     A                                      ROLLUP
     A                                      ROLLDOWN
     A                                      RTNCSRLOC(*RECNAME &DFRCD010 &DFFLD-
     A                                      010 &DFPOS010)
     A                                      RTNCSRLOC(*MOUSE &DFROW010 &DFCOL01-
     A                                      0)
     A  90 91                               CSRLOC(DFROW010   DFCOL010)
     A                                      OVERLAY
     A N60N61                               SFLDSP
     A N60                                  SFLDSPCTL
     A  60                                  SFLCLR
     A            DFRRN010       4S 0H      SFLRCDNBR(CURSOR)
     A            DFROW010       3S 0H
     A            DFCOL010       3S 0H
     A            DFPOS010       4S 0H
     A            DFFLD010      10A  H
     A            DFRCD010      10A  H
     A            DFPOSLIB  R        H      REFFLD(FLDGENCLS/XILIB *LIBL/FLDGEN-
     A                                      CLS)
     A            DFPOSFILE R        H      REFFLD(FLDGENCLS/XIFILE *LIBL/FLDGE-
     A                                      NCLS)
     A            DFPOSFLD  R        H      REFFLD(FLDGENCLS/XIFLD *LIBL/FLDGEN-
     A                                      CLS)
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A                                  1 11MSGCON(112 CON0001 *LIBL/RF0023MF)
     A                                      DSPATR(HI)
     A                                  1124'RF0023PG'
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(030 CON0011 *LIBL/RF0023MF)
     A            DFJOB     R        O  3 33REFFLD(FLDGENCLS/XIJOB *LIBL/FLDGEN-
     A                                      CLS)
     A                                  5  2MSGCON(130 CON0019 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  01                              7  2MSGCON(130 CON0030 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  01                              8  2MSGCON(130 CON0031 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  01                              9  2MSGCON(130 CON0032 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  01                             10  2MSGCON(130 CON0033 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A                                 17  2MSGCON(130 CON0002 *LIBL/RF0023MF)
     A                                      DSPATR(HI)
     A  02                              7  2MSGCON(130 CON0020 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                              8  2MSGCON(130 CON0021 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                              9  2MSGCON(130 CON0022 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                             10  2MSGCON(130 CON0023 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                             11  2MSGCON(130 CON0024 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                             12  2MSGCON(130 CON0025 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                             13  2MSGCON(130 CON0026 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                             14  2MSGCON(130 CON0027 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A  02                             15  2MSGCON(130 CON0028 *LIBL/RF0023MF)
     A                                      COLOR(RED)
     A          R DSPF010L
     A*%%TS  SD  20230524  140022  HILDEBRAND  REL-V7R5M0  5770-WDS
     A                                      OVERLAY
     A                                 19  2MSGCON(130 CON0008 *LIBL/RF0023MF)
     A          R DSPF010F
     A*%%TS  SD  20141020  122002  HILDEBRAND  REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A N61N62                          25117MSGCON(015 CON0013 *LIBL/RF0023MF)
     A                                      DSPATR(HI)
     A N61 62                          25117MSGCON(015 CON0014 *LIBL/RF0023MF)
     A                                      DSPATR(HI)
     A                                 26  2MSGCON(130 CON0006 *LIBL/RF0023MF)
     A                                      COLOR(BLU)
     A          R DSPF999S                  SFL
     A*%%TS  SD  20010425  174353  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(27)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ(276)
     A          R DSPF999C                  SFLCTL(DSPF999S)
     A*%%TS  SD  20010831  100809  HILDEBRAND  REL-V4R2M0  5769-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  50                                  SFLEND
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0001)
     A            DFPGMQ                    SFLPGMQ(276)
