     A*%%TS  SD  20171220  154614  HILDEBRAND  REL-V7R3M0  5770-WDS
     A*------------------------------------------------------------------------*
     A*                                                                        *
     A* COPYRIGHT (C) 2014 TASK FORCE IT-CONSULTING GMBH, WALTROP (GERMANY)    *
     A* This software and the accompanying materials are made available        *
     A* under the terms of the GNU General Public License version 2.0 (GPLv2)  *
     A* which accompanies this distribution and is available at                *
     A* http://www.gnu.org/licenses/gpl-2.0.html                               *
     A*                                                                        *
     A*------------------------------------------------------------------------*
     A*  >>PRE-COMPILER<<                                                      *
     A*    >>CRTCMD<< CRTDSPF    FILE(&LI/&OB) +                               *
     A*                            SRCFILE(&SL/&SF) SRCMBR(&SM);               *
     A*    >>IMPORTANT<<                                                       *
     A*      >>PARM<<  OPTION(*EVENTF);                                        *
     A*    >>END-IMPORTANT<<                                                   *
     A*    >>EXECUTE<<                                                         *
     A*  >>END-PRE-COMPILER<<                                                  *
     A*------------------------------------------------------------------------*
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT
     A          R DSPF010
     A*%%TS  SD  20150522  153959  HILDEBRAND  REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF01
     A  01
     AO 02
     AO 03                                  CF04
     A                                      RTNCSRLOC(*RECNAME &DFRCD010 &DFFLD-
     A                                      010 &DFPOS010)
     A                                      RTNCSRLOC(*MOUSE &DFROW010 &DFCOL01-
     A                                      0)
     A  90 91                               CSRLOC(DFROW010   DFCOL010)
     A                                      OVERLAY
     A            DFPOS010       4S 0H
     A            DFFLD010      10A  H
     A            DFRCD010      10A  H
     A            DFROW010       3S 0H
     A            DFCOL010       3S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A  01                              1 11MSGCON(060 CON0001 *LIBL/RF0015MF)
     A                                      DSPATR(HI)
     A  02                              1 11MSGCON(060 CON0002 *LIBL/RF0015MF)
     A                                      DSPATR(HI)
     A  03                              1 11MSGCON(060 CON0003 *LIBL/RF0015MF)
     A                                      DSPATR(HI)
     A  04                              1 11MSGCON(060 CON0004 *LIBL/RF0015MF)
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  05                              1 11MSGCON(060 CON0005 *LIBL/RF0015MF)
     A                                      DSPATR(HI)
     A                                  1 72'RF0015PG'
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(020 CON0007 *LIBL/RF0015MF)
     A            DFJOB     R        O  3 23REFFLD(SUBTOBENOT/XCJOB *LIBL/SUBTO-
     A                                      BENOT)
     A                                  5  2MSGCON(020 CON0010 *LIBL/RF0015MF)
     A            DFPOS     R     Y  B  5 23REFFLD(SUBTOBENOT/XCPOS *LIBL/SUBTO-
     A                                      BENOT)
     A  01
     AO 03                                  DSPATR(UL)
     A N01N03                               DSPATR(PR)
     A N87N88 89                            DSPATR(RI)
     A N87N88 89                            DSPATR(PC)
     A                                      EDTCDE(Z)
     A                                  5 37MSGCON(030 CON0017 *LIBL/RF0015MF)
     A                                  7  2MSGCON(020 CON0012 *LIBL/RF0015MF)
     A            DFTYPE    R        B  7 23REFFLD(SUBTOBENOT/XCTYPE *LIBL/SUBT-
     A                                      OBENOT)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A  87N88N89                            DSPATR(RI)
     A  87N88N89                            DSPATR(PC)
     A                                  7 37MSGCON(030 CON0014 *LIBL/RF0015MF)
     A                                  9  2MSGCON(020 CON0009 *LIBL/RF0015MF)
     A            DFUSR     R        B  9 23REFFLD(SUBTOBENOT/XCUSR *LIBL/SUBTO-
     A                                      BENOT)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88N89
     AON87 88 89                            DSPATR(RI)
     A N87 88N89
     AON87 88 89                            DSPATR(PC)
     A                                  9 37MSGCON(030 CON0015 *LIBL/RF0015MF)
     A                                 11  2MSGCON(020 CON0018 *LIBL/RF0015MF)
     A            DFMSGQ    R        B 11 23REFFLD(SUBTOBENOT/XCMSGQ *LIBL/SUBT-
     A                                      OBENOT)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A  87 88N89                            DSPATR(RI)
     A  87 88N89                            DSPATR(PC)
     A                                 11 37MSGCON(030 CON0020 *LIBL/RF0015MF)
     A                                 12  2MSGCON(020 CON0019 *LIBL/RF0015MF)
     A            DFMSGQL   R        B 12 23REFFLD(SUBTOBENOT/XCMSGQL *LIBL/SUB-
     A                                      TOBENOT)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A  87 88 89                            DSPATR(RI)
     A  87 88 89                            DSPATR(PC)
     A                                 12 37MSGCON(030 CON0021 *LIBL/RF0015MF)
     A N01N02N03                       23  2MSGCON(078 CON0006 *LIBL/RF0015MF)
     A                                      COLOR(BLU)
     A  01
     AO 02
     AO 03                             23  2MSGCON(078 CON0013 *LIBL/RF0015MF)
     A                                      COLOR(BLU)
     A          R DSPF020W
     A*%%TS  SD  20010719  145903  HILDEBRAND  REL-V4R2M0  5769-PW1
     A                                      CA12
     A                                      OVERLAY
     A                                      WINDOW(10 1 3 76)
     A                                  2  1MSGCON(076 CON0008 *LIBL/RF0015MF)
     A                                      DSPATR(BL)
     A          R DSPF999S                  SFL
     A*%%TS  SD  20010425  174353  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ(276)
     A          R DSPF999C                  SFLCTL(DSPF999S)
     A*%%TS  SD  20010430  173405  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  50                                  SFLEND
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0001)
     A            DFPGMQ                    SFLPGMQ(276)
