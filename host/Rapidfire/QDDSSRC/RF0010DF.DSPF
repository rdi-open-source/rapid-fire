     A*%%TS  SD  20171220  154614  HILDEBRAND  REL-V7R3M0  5770-WDS
     A*------------------------------------------------------------------------*
     A*                                                                        *
     A* COPYRIGHT (C) 2014 TASK FORCE IT-CONSULTING GMBH, WALTROP (GERMANY)    *
     A* This software and the accompanying materials are made available        *
     A* under the terms of the GNU General Public License version 2.0 (GPLv2)  *
     A* which accompanies this distribution and is available at                *
     A* http://www.gnu.org/licenses/gpl-2.0.html                               *
     A*                                                                        *
     A*------------------------------------------------------------------------*
     A*  >>PRE-COMPILER<<                                                      *
     A*    >>CRTCMD<< CRTDSPF    FILE(&LI/&OB) +                               *
     A*                            SRCFILE(&SL/&SF) SRCMBR(&SM);               *
     A*    >>IMPORTANT<<                                                       *
     A*      >>PARM<<  OPTION(*EVENTF);                                        *
     A*    >>END-IMPORTANT<<                                                   *
     A*    >>EXECUTE<<                                                         *
     A*  >>END-PRE-COMPILER<<                                                  *
     A*------------------------------------------------------------------------*
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT
     A          R DSPF010
     A*%%TS  SD  20141027  171829  HILDEBRAND  REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      CF01
     A  01
     AO 02
     AO 03                                  CF04
     A                                      RTNCSRLOC(*RECNAME &DFRCD010 &DFFLD-
     A                                      010 &DFPOS010)
     A                                      RTNCSRLOC(*MOUSE &DFROW010 &DFCOL01-
     A                                      0)
     A  90 91                               CSRLOC(DFROW010   DFCOL010)
     A                                      OVERLAY
     A            DFPOS010       4S 0H
     A            DFFLD010      10A  H
     A            DFRCD010      10A  H
     A            DFROW010       3S 0H
     A            DFCOL010       3S 0H
     A                                  1  2DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A  01                              1 11MSGCON(060 CON0001 *LIBL/RF0010MF)
     A                                      DSPATR(HI)
     A  02                              1 11MSGCON(060 CON0002 *LIBL/RF0010MF)
     A                                      DSPATR(HI)
     A  03                              1 11MSGCON(060 CON0003 *LIBL/RF0010MF)
     A                                      DSPATR(HI)
     A  04                              1 11MSGCON(060 CON0004 *LIBL/RF0010MF)
     A                                      DSPATR(HI)
     A                                      DSPATR(BL)
     A  05                              1 11MSGCON(060 CON0005 *LIBL/RF0010MF)
     A                                      DSPATR(HI)
     A                                  1 72'RF0010PG'
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(035 CON0007 *LIBL/RF0010MF)
     A            DFJOB     R        O  3 38REFFLD(CNVS/XBJOB *LIBL/CNVS)
     A                                  4  2MSGCON(035 CON0014 *LIBL/RF0010MF)
     A            DFPOS     R        O  4 38REFFLD(CNVS/XBPOS *LIBL/CNVS)
     A                                      EDTCDE(Z)
     A                                  6  2MSGCON(035 CON0010 *LIBL/RF0010MF)
     A            DFFTC     R        B  6 38REFFLD(CNVS/XBFTC *LIBL/CNVS)
     A  01
     AO 03                                  DSPATR(UL)
     A N01N03                               DSPATR(PR)
     A N87N88 89                            DSPATR(RI)
     A N87N88 89                            DSPATR(PC)
     A                                  6 52MSGCON(020 CON0009 *LIBL/RF0010MF)
     A                                  8  2MSGCON(035 CON0011 *LIBL/RF0010MF)
     A            DFRFT     R        B  8 38REFFLD(CNVS/XBRFT *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A  87N88 89                            DSPATR(RI)
     A  87N88 89                            DSPATR(PC)
     A                                  8 52MSGCON(020 CON0012 *LIBL/RF0010MF)
     A                                  9  2MSGCON(078 CON0015 *LIBL/RF0010MF)
     A                                      COLOR(RED)
     A                                 11  2MSGCON(060 CON0017 *LIBL/RF0010MF)
     A            DFSTM1    R        B 12  2REFFLD(CNVS/XBSTM1 *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88 89                            DSPATR(RI)
     A N87 88 89                            DSPATR(PC)
     A                                      CHECK(LC)
     A            DFSTM2    R        B 13  2REFFLD(CNVS/XBSTM2 *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88 89                            DSPATR(RI)
     A                                      CHECK(LC)
     A            DFSTM3    R        B 14  2REFFLD(CNVS/XBSTM3 *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88 89                            DSPATR(RI)
     A                                      CHECK(LC)
     A            DFSTM4    R        B 15  2REFFLD(CNVS/XBSTM4 *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88 89                            DSPATR(RI)
     A                                      CHECK(LC)
     A            DFSTM5    R        B 16  2REFFLD(CNVS/XBSTM5 *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88 89                            DSPATR(RI)
     A                                      CHECK(LC)
     A            DFSTM6    R        B 17  2REFFLD(CNVS/XBSTM6 *LIBL/CNVS)
     A  01
     AO 02
     AO 03                                  DSPATR(UL)
     A N01N02N03                            DSPATR(PR)
     A N87 88 89                            DSPATR(RI)
     A                                      CHECK(LC)
     A                                 18  2MSGCON(078 CON0016 *LIBL/RF0010MF)
     A                                      COLOR(RED)
     A N01N02N03                       23  2MSGCON(078 CON0006 *LIBL/RF0010MF)
     A                                      COLOR(BLU)
     A  01
     AO 02
     AO 03                             23  2MSGCON(078 CON0013 *LIBL/RF0010MF)
     A                                      COLOR(BLU)
     A          R DSPF020W
     A*%%TS  SD  20010719  145903  HILDEBRAND  REL-V4R2M0  5769-PW1
     A                                      CA12
     A                                      OVERLAY
     A                                      WINDOW(10 1 3 76)
     A                                  2  1MSGCON(076 CON0008 *LIBL/RF0010MF)
     A                                      DSPATR(BL)
     A          R DSPF999S                  SFL
     A*%%TS  SD  20010425  174353  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLMSGRCD(24)
     A            DFMSGK                    SFLMSGKEY
     A            DFPGMQ                    SFLPGMQ(276)
     A          R DSPF999C                  SFLCTL(DSPF999S)
     A*%%TS  SD  20010430  173405  EXT01       REL-V4R4M0  5769-PW1
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  50                                  SFLEND
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0001)
     A            DFPGMQ                    SFLPGMQ(276)
