     /*-------------------------------------------------------------------------------------------*/
     /*                                                                                           */
     /* Copyright (c) 2014 Task Force IT-Consulting GmbH, Waltrop (Germany)                       */
     /* This software and the accompanying materials are made available                           */
     /* under the terms of the GNU General Public License version 2.0 (GPLv2)                     */
     /* which accompanies this distribution and is available at                                   */
     /* http://www.gnu.org/licenses/gpl-2.0.html                                                  */
     /*                                                                                           */
     /*-------------------------------------------------------------------------------------------*/
     /*                                                                                           */
     /* Create data area 'HOME', which contains the Rapid Fire home library.                      */
     /*                                                                                           */
     /*-------------------------------------------------------------------------------------------*/
     /* STRPREPRC Compile Options:                                                                */
     /* >>PRE-COMPILER<<                                                                          */
     /*   >>CRTCMD<<  CALL       PGM(QTEMP/&OB) PARM(&LI &OB);                                    */
     /*   >>CMD<<     CRTCLPGM   PGM(QTEMP/&OB) +                                                 */
     /*                          SRCFILE(&SL/&SF) SRCMBR(&SM) +                                   */
     /*               TEXT('Rapid Fire Home Library');                                            */
     /*   >>EXECUTE<<                                                                             */
     /*   >>CMD<<     DLTPGM PGM(QTEMP/&OB);                                                      */
     /* >>END-PRE-COMPILER<<                                                                      */
     /*-------------------------------------------------------------------------------------------*/
             PGM        PARM(&LIB &OBJ)

             DCL        VAR(&LIB ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OBJ ) TYPE(*CHAR) LEN(10)

             DCL        VAR(&HOME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HOME_LIB) TYPE(*CHAR) STG(*DEFINED) LEN(10) DEFVAR(&HOME 1)

             /* ------------------------- Error handler ------------------------- */
             DCL        VAR(&ERRMSGKEY ) TYPE(*CHAR) LEN( 4)
             DCL        VAR(&ERRMSGTYPE) TYPE(*CHAR) LEN(10) VALUE('*DIAG')
             DCL        VAR(&ERRNUMTYPE) TYPE(*CHAR) LEN( 4) VALUE(x'00000001')
             DCL        VAR(&ERRSTKE   ) TYPE(*CHAR) LEN(10) VALUE('*PGMBDY')
             DCL        VAR(&ERRSTKCNT ) TYPE(*CHAR) LEN( 4) VALUE(x'00000001')
             DCL        VAR(&ERRCODE   ) TYPE(*CHAR) LEN( 8) VALUE(x'0000000000000000')

             MONMSG     MSGID(CPF0000) EXEC(GOTO ERRHDLR)
             /* ----------------------------------------------------------------- */

             CHGVAR     VAR(&HOME_LIB) VALUE(&LIB)

             DLTDTAARA  DTAARA(&LIB/&OBJ)
             MONMSG     MSGID(CPF2105) EXEC(DO)
                RMVMSG     PGMQ(*SAME (*)) CLEAR(*ALL)
             ENDDO

             CRTDTAARA  DTAARA(&LIB/&OBJ) TYPE(*CHAR) LEN(10) +
                          TEXT('Rapid Fire Home Library')

             CHGDTAARA  DTAARA(&LIB/&OBJ (*ALL)) VALUE(&HOME)

             RETURN

             /* --------------------------------------------------- */
             /*   Error Handler                                     */
             /* --------------------------------------------------- */
 ERRHDLR:    CALL       PGM(QMHMOVPM) PARM(&ERRMSGKEY &ERRMSGTYPE &ERRNUMTYPE &ERRSTKE &ERRSTKCNT &ERRCODE    )
             MONMSG     MSGID(CPF0000)

             CALL       PGM(QMHRSNEM) PARM(&ERRMSGKEY &ERRCODE    )

             ENDPGM
